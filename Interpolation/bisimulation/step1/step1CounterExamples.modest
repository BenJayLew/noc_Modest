//version of the concrete model used to calculate transition probabilities for the 13 state model

datatype buffer = {int(-1..3) val, int(0..3) id, bool serviced, int(0..3) priority, int timesServiced};		//Datatype for a buffer with the next buffer value, id of the buffer, whether the buffer was serviced last clock cycle or not, and the priority number of the buffer
int unserviced;		//Number of unserviced buffers, iterates down when unserviced buffer's priority is set
int totalUnserviced;	//Total number of unserviced buffers in a clock cycle
int totalServiced = 0;
int optimalRuns = 0;
const int clkLower = 100; 
const int clkUpper = 102;
int(clkLower..clkUpper) clk;

const int val0;
const int val1;
const int val2;
const int val3;

int(0..5) state = 0; 		//0-S0, 1-S1, 2-S3, 3-S2a, 4-S2b, 5-S2c
int(0..5) pstate;

buffer north, east, south, west;

buffer[] a; // = [north, east, south, west]; 	//Array of the four buffers. a[0] is the highest priority
buffer[] temp;		//Temporary buffer used to update buffer a

action tick;

// ------ Begin: properties ---------
//property Pr1 = Pmax(<> (clk >= clkUpper));

//property TotalServiced = Pmax(<> (totalServiced >= ((clkUpper-clkLower)*3)));		//Probablility that the arbiter servieces at least 15 buffers out of a possible 20. probablity the arbiter is at least 75% efficient

//property conflict1 = Pmax(<> (totalUnserviced == 1 && clk >= clkUpper));

//property conflict2 = Pmax(<> (totalUnserviced == 2 && clk >= clkUpper));

//property triway = Pmax(<> (tri == true && clk >= clkUpper));

//1 clock cycle

property State0 = Pmax(<> ((clk >= clkUpper) && (state == 0)));
property State1 = Pmax(<> ((clk >= clkUpper) && (state == 1)));
property State2 = Pmax(<> ((clk >= clkUpper) && (state == 2)));
property State3 = Pmax(<> ((clk >= clkUpper) && (state == 3)));
property State4 = Pmax(<> ((clk >= clkUpper) && (state == 4)));
property State5 = Pmax(<> ((clk >= clkUpper) && (state == 5)));


//property Runs1 = Pmax(<> (optimalRuns == 1));		//Probablility that there was at least 1 cycle where all buffers were serviced
//property Runs2 = Pmax(<> (optimalRuns == 2));		//Probablility that there was at least 2 cycles where all buffers were serviced
//property Runs3 = Pmax(<> (optimalRuns == 3));		//Probablility that there was at least 3 cycles where all buffers were serviced
//property Runs4 = Pmax(<> (optimalRuns == 4));		//Probablility that there was at least 4 cycles where all buffers were serviced
//property Runs5 = Pmax(<> (optimalRuns == 5));		//Probablility that there was at least 5 cycles where all buffers were serviced
// ------ End: properties ---------

process Clock(){	
	/*
	do {
	tick
	}
	*/
	tick {= clk++ =};
	if (clk <= clkUpper) {
	   Clock()
	}
	else {
	   stop
	}   
	
}


//Main process for the arbiter -------------------------------------------------------------------------------------------------------------------------------------------------
process Arbiter(){
	initialize();
	
	//runtime loop
	do {
		
		{= pstate = state =};	
	
		if((a[0].val == a[1].val && a[2].val == a[1].val) || (a[0].val == a[1].val && a[1].val == a[3].val) || (a[0].val == a[2].val && a[2].val == a[3].val) || (a[1].val == a[2].val && a[2].val == a[3].val)) { 
			{= state = 2 =}
		}
		else if (
		((a[0].val == a[2].id) && (a[1].val == a[3].id) && (a[2].val == a[3].id) && (a[3].val == a[2].id)) || 
		((a[0].val == a[3].id) && (a[1].val == a[2].id) && (a[2].val == a[3].id) && (a[3].val == a[2].id)) || 
		((a[0].val == a[3].id) && (a[1].val == a[3].id) && (a[2].val == a[1].id) && (a[3].val == a[1].id))
		){
			{= state = 3 =}
		}
		else if (
		((a[0].val == a[1].id) && (a[1].val == a[0].id) && (a[2].val == a[0].id) && (a[3].val == a[1].id)) || 
		((a[0].val == a[1].id) && (a[1].val == a[0].id) && (a[2].val == a[1].id) && (a[3].val == a[0].id)) || 
		((a[0].val == a[2].id) && (a[1].val == a[2].id) && (a[2].val == a[0].id) && (a[3].val == a[0].id))
		){
			{= state = 4 =}
		}
		else if (
		((a[0].val == a[1].id) && (a[1].val == a[2].id) && (a[2].val == a[1].id) && (a[3].val == a[2].id)) || 
		((a[0].val == a[1].id) && (a[1].val == a[3].id) && (a[2].val == a[3].id) && (a[3].val == a[1].id)) || 
		((a[0].val == a[2].id) && (a[1].val == a[2].id) && (a[2].val == a[1].id) && (a[3].val == a[1].id)) ||
		((a[0].val == a[2].id) && (a[1].val == a[0].id) && (a[2].val == a[0].id) && (a[3].val == a[2].id)) || 
		((a[0].val == a[3].id) && (a[1].val == a[3].id) && (a[2].val == a[0].id) && (a[3].val == a[0].id)) || 
		((a[0].val == a[3].id) && (a[1].val == a[0].id) && (a[2].val == a[3].id) && (a[3].val == a[0].id)) 
		){
			{= state = 5 =}
		}
		else{
			tau
		};
		
		//Service buffers-------------------------------------------------------------------------------------------------------
		
		//Buffer priority 0
		//<Service Buffer a[0]>--------------------------------------------------------
		{= 
		a[0].timesServiced++,
		a[0].serviced = true 
		=};
		
		//<Service buffer <a[1]> ------------------------------------------------------
		if( a[1].val == a[0].val) {			//If buffer a[1] conflicts with a[0]
			{= 
			a[1].serviced = false,
			unserviced++ 		//Increment unserviced
			=}
		}
		else {
			{= 
			a[1].timesServiced++,		//Service Buffer
			a[1].serviced = true 
			=}
		};
		
		//<Service buffer a[2]> --------------------------------------------------------
		//If buffer a[2] conflicts with a[1] or a[0]
		if(a[2].val == a[1].val || a[2].val == a[0].val){	
			{= 
			a[2].serviced = false, 
			unserviced++			//Increment unserviced
			=}
		}
		else {
			{= 
			a[2].timesServiced++,		//Service Buffer
			a[2].serviced = true 
			=}
		};
		
		//<Service buffer a[3]> ---------------------------------------------------------
		//If buffer a[3] conflicts with a[2], a[1], or a[0]
		if( a[3].val == a[2].val || a[3].val == a[1].val || a[3].val == a[0].val){	
			{= 
			a[3].serviced = false,
			unserviced++ 			//Increment unserviced
			=}
		}
		else {
			{= 
			a[3].timesServiced++,		//Service Buffer
			a[3].serviced = true 
			=}
		};
		
		//Increment optimal runs if there were no conflitcs -------------------------------------------------------------------
		
		if(unserviced == 0) {
			{= 
			optimalRuns++,
			totalUnserviced = unserviced,
			totalServiced = totalServiced + 4 - unserviced
			=}
		}
		else{
			{= 
			totalUnserviced = unserviced,
			totalServiced = totalServiced + 4 - unserviced
			=}
		};
		
		
		
		//Set priority values for the next run and update the value of the buffers----------------------------------------------
		setPriority(0);
		setPriority(1);
		setPriority(2);
		setPriority(3);
		
		
		
		
		//Rewrite buffer array with new priority values-------------------------------------------------------------------------
		{= 
		temp[a[0].priority] = a[0],
		temp[a[1].priority] = a[1],
		temp[a[2].priority] = a[2],
		temp[a[3].priority] = a[3] 
		=};
		
		{= a = temp =};
		
		//set state variable
		if(totalUnserviced == 0){			//0
			{= state = 0 =}
		}
		else if(totalUnserviced == 1){			//1
			{= state = 1 =}
		}
		else{
			tau
		};
		tick;
		
		if (clk >= clkUpper) {
			stop
		}
		else {
			tau
		}
		
		
	}
}

//initialize values -------------------------------------------------------------------------------------------------------------------------------------------------------------
process initialize(){
	{= 
	a = [north, east, south, west],
	temp = [north, east, south, west], 
	
	a[0].id = 0, 
	a[1].id = 1, 
	a[2].id = 2, 
	a[3].id = 3,  
	
	
	a[0].val = val0,
	a[1].val = val1,
	a[2].val = val2,
	a[3].val = val3,
	
	
	unserviced = 0
	=}

	
}

//Sets the new priority value of buffer a[i] and updates the value if the buffer was serviced ------------------------------------------------------------------------------------
process setPriority(int i){
	if (a[i].serviced == true){
		{= 
		a[i].val = (DiscreteUniform(1, 3) + a[i].id) % 4,	//Update value	
		a[i].priority = i + unserviced 						//Set new priority
		=}
		//<update buffer value and dequeue buffer>
	}
	else {
		{= 
		a[i].priority = totalUnserviced - unserviced,		//Set new priority
		unserviced-- 						//decrement unserviced
		=}
	}
}


par{
::	Clock()
::	Arbiter()
}
