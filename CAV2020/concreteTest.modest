/*
NoC router IDs
	0 - 1
	|   |
	2 - 3
*/

option "dtmc";

//Top level varables
const int BUFFERLENGTH = 3; //Each channel actually has length of BUFFERLENGTH + 1 because one value is stored in the arbiter itself for easy use when routing.
const int NOCSIZE = 2;
const int OPTIMALRUNSMEASURE = 4;
const int INJECTIONRATENUMERATOR = 1;
const int INJECTIONRATEDENOMINATOR = 2;

const int DUR;
const int clkLower = 100; 
const int clkUpper = clkLower + DUR;
int(clkLower..clkUpper) clk;
//transient int(0..1) clk;

int test = 0;

datatype intlist = { int(0.. ((NOCSIZE*NOCSIZE)-1)) hd, intlist option tl };

datatype channel = {int(-1..3) dest, int(-1..4) direction, int(0..4) id, bool serviced, int(0..4) priority, intlist option buff};		//Datatype for a channel with the next channel value, id of the channel, whether the channel was serviced last clock cycle or not, and the priority number of the channel

datatype router = {int(0..3) unserviced, int(0..3) totalUnserviced, channel[] channelArray, channel[] temp, int[]channelID};	//Datatype for a router, includes router-specific variables and an array of channels

router r0, r1, r2, r3;

int(0..5/*(clkUpper - clkLower)*/) optimalRuns;

router[] noc;

action tick;


// ------ Begin: properties ---------
property Pr1 = Pmax(<> ((optimalRuns) >= 12));
//property p1 = Pmax(<>[S(clk)<=(clkUpper-clkLower)] (optimalRuns0 >= 1));
//property optimalRuns0 = Pmax(<> (optimalRuns0 >= OPTIMALRUNSMEASURE));
//property optimalRuns1 = Pmax(<> (optimalRuns1 >= OPTIMALRUNSMEASURE));
//property optimalRuns2 = Pmax(<> (optimalRuns2 >= OPTIMALRUNSMEASURE));
//property optimalRuns3 = Pmax(<> (optimalRuns3 >= OPTIMALRUNSMEASURE));
// ------ End: properties ---------



// ------ Begin: function definitions ------- 
// Calculate length of list:
function int len(intlist option ls) = if ls == none then 0 else 1 + len(ls!.tl); // "!" accesses the value of a non-empty option (causes an error if the option is empty)

// Return the front of the queue, i.e., the last element of the given integer list. No changes are made to the list.
function int peekFront(intlist option ls) =
	 if ls == none then -1
	 else if ls!.tl == none then ls!.hd
	      else peekFront(ls!.tl);

//Remove the front of the queue, i.e., the last element of the given integer list. Return the modified list. 
function intlist option dequeue(intlist option ls) = 
	if ls == none then none
	else if	ls!.tl == none then none 
		 else some(intlist { hd: ls!.hd, tl: dequeue(ls!.tl) });	

//Insert a new entry n as the head of the list ls. If ls has already reached its capacity before adding n, return ls; otherwise, return modified ls with n as its head. 
function intlist option enqueue(int n, intlist option ls) = 
	if len(ls) == BUFFERLENGTH then ls
	else some(intlist { hd: n, tl: ls });		 

//Routing function	(x-y routing)
function int route(int dest, int routerID) =
	if dest == -1 then -1//no flit
	else if (dest == routerID) then 4//local
	else if (dest % NOCSIZE) > (routerID % NOCSIZE) then 1/*east*/
	else if (dest % NOCSIZE) < (routerID % NOCSIZE) then 3/*west*/
	else if (dest / NOCSIZE) > (routerID / NOCSIZE) then 2/*south*/
	else if (dest / NOCSIZE) < (routerID / NOCSIZE) then 0/*north*/
	else 4//local
	;

function bool checkNotFull(int direction, int routerID) = 
	if direction == -1 then true //no flit
	else if (direction == 0 && routerID - NOCSIZE >= 0) then len(noc[routerID - NOCSIZE].channelArray[noc[routerID - NOCSIZE].channelID[2]].buff) < BUFFERLENGTH
	else if (direction == 1  && routerID + 1 <= 3) then len(noc[routerID + 1].channelArray[noc[routerID + 1].channelID[3]].buff) < BUFFERLENGTH
	else if (direction == 2  && routerID + NOCSIZE <= 3) then len(noc[routerID + NOCSIZE].channelArray[noc[routerID + NOCSIZE].channelID[0]].buff) < BUFFERLENGTH
	else if (direction == 3  && routerID - 1 >= 0) then len(noc[routerID - 1].channelArray[noc[routerID - 1].channelID[3]].buff) < BUFFERLENGTH
	else if direction == 4 then true //send local
	else false //catch all case
	;
	
function int(0..1) checkNotFullIncrement(int direction, int routerID) = (checkNotFull(direction, routerID)) ? 0 : 1;


// ------ Begin: process definitions ------
//---- Paralell Processes ----
process Clock(){	
	//tick {= clk=1 =};
	//Clock()	

	
	tick {= clk++ =};
	if (clk <= clkUpper) {
	   Clock()
	}
	else {
	   stop
	} 
		
}

process Top(){
	initialize();
	Populate();
	//tick;		//for measuring initial values in testing and maybe syncing population of routers?
	initialRoute(0);
	initialRoute(1);
	initialRoute(2);
	initialRoute(3);
	do{
		Arbiter_corner(0);
		Arbiter_corner(1);
		Arbiter_corner(2);
		Arbiter_corner(3);	
		
		Populate();	
		
		updateDestination(0, 0);
		updateDestination(1, 0);
		updateDestination(2, 0);
		
		updateDestination(0, 1);
		updateDestination(1, 1);
		updateDestination(2, 1);
		
		updateDestination(0, 2);
		updateDestination(1, 2);
		updateDestination(2, 2);
		
		updateDestination(0, 3);
		updateDestination(1, 3);
		updateDestination(2, 3);
		
		tick;

		
		if(clk>=clkUpper){
			stop
		}
		else{
			tau
		} 
		
	}
}

process Populate(){

	if((clk % INJECTIONRATEDENOMINATOR) < INJECTIONRATENUMERATOR){
		subPopulate(0);
		subPopulate(1);
		subPopulate(2);
		subPopulate(3)
	}
	else {tau}
}


//---- Sub processes ----
process Arbiter_corner(int routerID){
	//Service channels-------------------------------------------------------------------------------------------------------
		
		//Buffer priority 0
		//<Service Buffer channelArray[0]>--------------------------------------------------------
		{=
		noc[routerID].channelArray[0].serviced = checkNotFull(noc[routerID].channelArray[0].direction, routerID),
		noc[routerID].totalUnserviced += checkNotFullIncrement(noc[routerID].channelArray[0].direction, routerID),
		noc[routerID].unserviced += checkNotFullIncrement(noc[routerID].channelArray[0].direction, routerID)
		=};
		
		//<Service buffer channelArray[1]> ------------------------------------------------------
		if(noc[routerID].channelArray[1].direction == -1 || noc[routerID].channelArray[1].direction != noc[routerID].channelArray[0].direction) {			//If buffer channelArray[1] does not conflict with channelArray[0]
			{= 	//Service Buffer
			noc[routerID].channelArray[1].serviced = checkNotFull(noc[routerID].channelArray[1].direction, routerID),
			noc[routerID].totalUnserviced += checkNotFullIncrement(noc[routerID].channelArray[1].direction, routerID),
			noc[routerID].unserviced += checkNotFullIncrement(noc[routerID].channelArray[1].direction, routerID)
			=}
		}
		else {
			{= 
			noc[routerID].channelArray[1].serviced = false,
			noc[routerID].totalUnserviced++,
			noc[routerID].unserviced++ 		//Increment unserviced
			=}
		};
		
		//<Service buffer channelArray[2]> --------------------------------------------------------
		//If buffer channelArray[2] does not conflict with channelArray[1] or channelArray[0]
		if(noc[routerID].channelArray[2].direction == -1 || noc[routerID].channelArray[2].direction != noc[routerID].channelArray[1].direction && noc[routerID].channelArray[2].direction != noc[routerID].channelArray[0].direction){	
			{= 	//Service Buffer
			noc[routerID].channelArray[2].serviced = checkNotFull(noc[routerID].channelArray[2].direction, routerID),
			noc[routerID].totalUnserviced += checkNotFullIncrement(noc[routerID].channelArray[2].direction, routerID),
			noc[routerID].unserviced += checkNotFullIncrement(noc[routerID].channelArray[2].direction, routerID)
			=}
		}
		else {
			{= 
			noc[routerID].channelArray[2].serviced = false, 
			noc[routerID].totalUnserviced++,
			noc[routerID].unserviced++			//Increment unserviced
			=}
		};
		
		
		//Increment optimal runs if all buffers forwarded a flit -------------------------------------------------------------------
		if(noc[routerID].unserviced == 0 && noc[routerID].channelArray[0].direction != -1 && noc[routerID].channelArray[1].direction != -1 && noc[routerID].channelArray[2].direction != -1){
			{= 
				optimalRuns++
			=}
		}
		else{tau};
		
		//Set priority values for the next run and update the value of the channels----------------------------------------------
		setPriority(0, routerID);
		setPriority(1, routerID);
		setPriority(2, routerID);
		
		
		//Rewrite channel array with new priority values-------------------------------------------------------------------------
		{= 
		noc[routerID].temp[noc[routerID].channelArray[0].priority] = noc[routerID].channelArray[0],
		noc[routerID].temp[noc[routerID].channelArray[1].priority] = noc[routerID].channelArray[1],
		noc[routerID].temp[noc[routerID].channelArray[2].priority] = noc[routerID].channelArray[2],
		
		noc[routerID].channelID[noc[routerID].channelArray[0].id] = noc[routerID].channelArray[0].priority,
		noc[routerID].channelID[noc[routerID].channelArray[1].id] = noc[routerID].channelArray[1].priority,
		noc[routerID].channelID[noc[routerID].channelArray[2].id] = noc[routerID].channelArray[2].priority
		=};
		
		{= noc[routerID].channelArray = noc[routerID].temp =}
}
	
//initialize values --------------------------------------------------------------------------------------------------------------------------------------------
process initialize(){
	{= noc = [r0, r1, r2, r3] =};
	
	initialize_node_nw(0);
	initialize_node_ne(1);
	initialize_node_sw(2);
	initialize_node_se(3)
}

process initialize_node_nw(int routerID){
	channel east, south, local;
	{=
	noc[routerID].channelArray = [east, south, local],
	noc[routerID].temp = [east, south, local],
	
	noc[routerID].channelID = [-1, 0, 1, -1, 2],
	
	noc[routerID].channelArray[0].id = 1, 
	noc[routerID].channelArray[1].id = 2, 
	noc[routerID].channelArray[2].id = 4,
	
	noc[routerID].channelArray[0].dest = -1,
	noc[routerID].channelArray[1].dest = -1,
	noc[routerID].channelArray[2].dest = -1,
	
	noc[routerID].unserviced = 0
	
	=}
}

process initialize_node_ne(int routerID){
	channel south, west, local;
	{=
	noc[routerID].channelArray = [south, west, local],
	noc[routerID].temp = [south, west, local], 
	
	noc[routerID].channelID = [-1, -1, 0, 1, 2],
	
	noc[routerID].channelArray[0].id = 2, 
	noc[routerID].channelArray[1].id = 3, 
	noc[routerID].channelArray[2].id = 4,
	
	noc[routerID].channelArray[0].dest = -1,
	noc[routerID].channelArray[1].dest = -1,
	noc[routerID].channelArray[2].dest = -1,
	
	noc[routerID].unserviced = 0
	=}
}

process initialize_node_sw(int routerID){
	channel north, east, local;
	{=
	noc[routerID].channelArray = [north, east, local],
	noc[routerID].temp = [north, east, local], 
	
	noc[routerID].channelID = [0, 1, -1, -1, 2],
	
	noc[routerID].channelArray[0].id = 0, 
	noc[routerID].channelArray[1].id = 1, 
	noc[routerID].channelArray[2].id = 4,
	
	noc[routerID].channelArray[0].dest = -1,
	noc[routerID].channelArray[1].dest = -1,
	noc[routerID].channelArray[2].dest = -1,
	
	noc[routerID].unserviced = 0
	=}
}

process initialize_node_se(int routerID){
	channel north, west, local;
	{=
	noc[routerID].channelArray = [north, west, local],
	noc[routerID].temp = [north, west, local], 
	
	noc[routerID].channelID = [0, -1, -1, 1, 2],
	
	noc[routerID].channelArray[0].id = 0, 
	noc[routerID].channelArray[1].id = 3, 
	noc[routerID].channelArray[2].id = 4,
	
	noc[routerID].channelArray[0].dest = -1,
	noc[routerID].channelArray[1].dest = -1,
	noc[routerID].channelArray[2].dest = -1,
	
	noc[routerID].unserviced = 0
	=}
	
}

//runs routing after initial setup (for testing)
process initialRoute(int routerID){
	
	{=
	noc[routerID].channelArray[0].direction = route(noc[routerID].channelArray[0].dest, routerID),
	noc[routerID].channelArray[1].direction = route(noc[routerID].channelArray[1].dest, routerID),
	noc[routerID].channelArray[2].direction = route(noc[routerID].channelArray[2].dest, routerID)
	=}
}


//Sets the new priority value of channel channelArray[i] and forwards the detination if the channel was serviced ------------------------------------------------------------------
process setPriority(int i, int routerID){
	if (noc[routerID].channelArray[i].serviced == true){
		if(noc[routerID].channelArray[i].direction == -1){ tau }
		else if(noc[routerID].channelArray[i].direction == 4){			//Send in the local direction
			tau		//flit delivered
		}
		else if(noc[routerID].channelArray[i].direction == 0){			//Send in the north direction
			{=
			noc[routerID - NOCSIZE].channelArray[noc[routerID - NOCSIZE].channelID[2]].buff = enqueue(noc[routerID].channelArray[i].dest, noc[routerID - NOCSIZE].channelArray[noc[routerID - NOCSIZE].channelID[2]].buff)
			=}
		}
		else if(noc[routerID].channelArray[i].direction == 1){			//Send in the east direction
			{=
			noc[routerID + 1].channelArray[noc[routerID + 1].channelID[3]].buff = enqueue(noc[routerID].channelArray[i].dest, noc[routerID + 1].channelArray[noc[routerID + 1].channelID[3]].buff)
			=}
		}
		else if(noc[routerID].channelArray[i].direction == 2){			//Send in the south direction
			{=
			noc[routerID + NOCSIZE].channelArray[noc[routerID + NOCSIZE].channelID[0]].buff = enqueue(noc[routerID].channelArray[i].dest, noc[routerID + NOCSIZE].channelArray[noc[routerID + NOCSIZE].channelID[0]].buff)
			=}
		}
		else if(noc[routerID].channelArray[i].direction == 3){			//Send in the west direction
			{=
			noc[routerID - 1].channelArray[noc[routerID - 1].channelID[1]].buff = enqueue(noc[routerID].channelArray[i].dest, noc[routerID - 1].channelArray[noc[routerID - 1].channelID[1]].buff)
			=}
		}
		else {tau};
		
		{=
		noc[routerID].channelArray[i].priority = i + noc[routerID].unserviced 				//Set new priority
		=}
	}
	else {
		{= 
		noc[routerID].channelArray[i].priority = noc[routerID].totalUnserviced - noc[routerID].unserviced,		//Set new priority
		noc[routerID].unserviced-- 						//decrement unserviced
		=}
	}
}

//Updates the destination from the channel if the channel was serviced
process updateDestination(int i , int routerID){
	if(noc[routerID].channelArray[i].serviced == true){
		{= 
		noc[routerID].channelArray[i].dest = peekFront(noc[routerID].channelArray[i].buff)
		=};
		{=
		noc[routerID].channelArray[i].direction = route(noc[routerID].channelArray[i].dest, routerID),		//Update value from channel
		test = 17,
		noc[routerID].channelArray[i].buff = dequeue(noc[routerID].channelArray[i].buff)
		=}
	}
	else {{=test = 16=}}
}

//Populate the channels with new values
process subPopulate(int routerID){
	{= noc[routerID].channelArray[noc[routerID].channelID[4]].buff = enqueue(((DiscreteUniform(1, 3) + routerID) % 4), noc[routerID].channelArray[noc[routerID].channelID[4]].buff) =}
}
// ------- End: process definitions -------

par{
::	Clock()
:: 	Top()
}

