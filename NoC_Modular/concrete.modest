/****************************************************************************************************************************
 * This is a modularized version of the concrete model for NoC model verification.
 *
 * Current NoC router IDs
 *	0 - 1
 *	|   |
 *	2 - 3
 *
 * In order to add routers to the NoC visit all coments labled with #MODULARIZE
 *
 * Editors: Jonah Boe
 * Date: Nov 16, 2021
 ***************************************************************************************************************************/

option "dtmc";



// ------------------------------------------------------- Datatypes --------------------------------------------------------

datatype buffer =   {int(0..NOC_MAX_ID) hd,
							buffer option tl
};

datatype channel =  {buffer option buffer,
							bool serviced
};

datatype router =   {channel[] channels,
							int(-1..NOC_MAX_ID)[] ids,
							int(0..4)[] priority_list,
							bool[] used,
							int(0..5) total_unserviced,
							int(0..4)[] priority_list_temp,
							int(0..4) serviced_index,
							int(0..4) unserviced_index
};



// --------------------------------------------------- Top level varables ---------------------------------------------------

// #MODULARIZE by setting NOC_MAX_ID to 1 less than the total number of routers and allocating spaces for more routers
const int NOC_MAX_ID = 8;
router[] noc = [router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}];

// #MODULARIZE this value changes the buffer length of all buffers.
const int BUFFER_LENGTH = 3;

// #MODULARIZE clkUpper changes the upper bund of cycles run before quiting
const int CLK_UPPER = 1;
int CLK = 0;

// These values are enumerations and are NOT to be changed
const int NORTH = 0;
const int WEST = 1;
const int EAST = 2;
const int SOUTH = 3;
const int LOCAL = 4;



// ------------------------------------------------------- Properties -------------------------------------------------------

property finished = Pmax(<> (CLK == CLK_UPPER));



// -------------------------------------------------------- Actions ---------------------------------------------------------

// For keeping parallel processes synced
action tick;
action tock;



// ------------------------------------------------------- Functions --------------------------------------------------------

// Calculate length of list
function int len(buffer option ls) = if ls == none then 0 else 1 + len(ls!.tl);

// Get the mirror direction
function int getDestinationChannel(int dir) =
	if dir == NORTH then SOUTH
	else if dir == WEST then EAST
	else if dir == EAST then WEST
	else if dir == SOUTH then NORTH
	else -1;

// Determine if the flits destination is in the same column as the current router
function int getColumnShift(int id, int dst) =
   (dst % ((int)sqrt(NOC_MAX_ID + 1))) - (id % ((int)sqrt(NOC_MAX_ID + 1)));

// Tell if the buffer is full
function bool isBufferFull(int id, int dir) = len(noc[id].channels[dir].buffer) >= BUFFER_LENGTH;

// Return the front of the queue
function int peekFront(buffer option ls) =
	if ls == none then -1
	else if ls!.tl == none then ls!.hd
	else peekFront(ls!.tl);

// Add a flit to the buffer
function buffer option enqueue(int n, buffer option ls) =
	if len(ls) == BUFFER_LENGTH then ls
	else some(buffer {
		hd: n,
		tl: ls
	});

// Remove a flit from the buffer
function buffer option dequeue(buffer option ls) =
	if ls == none then none
	else if ls!.tl == none then none
	else some(buffer {
		hd: ls!.hd,
		tl: dequeue(ls!.tl)
	});



// ------------------------------------------------------- Processes --------------------------------------------------------

// Send flit to northern router. src and dst are channels (ie NORTH, WEST, EAST, SOUTH, LOCAL), NOT the actual router ids!
process Send(int id, int buf, int dir){
	// We want to chech the final destination buffer of the router we are headed into, to see if it is full
	int(-1..3) dst;
	{=
		// A flit going from router 0 to router 1 leaves 0 from the SOUTH and enters 1 from the NORTH.
		dst = getDestinationChannel(dir)
	=};

	if(!isBufferFull(noc[id].ids[dir], dst) && !noc[id].used[dir]){
		{=
			// First, add flit to destination buffer
			0: noc[noc[id].ids[dir]].channels[dst].buffer =
				enqueue(peekFront(noc[id].channels[buf].buffer), noc[noc[id].ids[dir]].channels[dst].buffer),
			// Then, remove it from the source buffer
			1: noc[id].channels[buf].buffer = dequeue(noc[id].channels[buf].buffer),
			// Mark that output as used and that channel as serviced
			2: noc[id].used[dir] = true,
			2: noc[id].channels[buf].serviced = true
		=}
	}
	else{
		// Incrament total unserviced buffers
		{=
			noc[id].total_unserviced++
		=}
	}
}

// Advance flits to respective buffers
process AdvanceFlits(int id, int buf){
	// If flit needs to stay on this row
	if(getColumnShift(id, peekFront(noc[id].channels[buf].buffer)) == 0){
		// Send it north
		if(peekFront(noc[id].channels[buf].buffer) < id){
			Send(id, buf, NORTH)
		}
		// Or send it south
		else{
			Send(id, buf, SOUTH)
		}
	}
	// Else, if flit needs to go west
	else if(getColumnShift(id, peekFront(noc[id].channels[buf].buffer)) < 0){
		Send(id, buf, WEST)
	}
	// Else, flit needs to go east
	else{
		Send(id, buf, EAST)
	}
}

// Update a specific channel
process AdvanceChannel(int id, int buf){
	if(peekFront(noc[id].channels[buf].buffer) == -1){
		{=
			// Mark the channel as serviced
			noc[id].channels[buf].serviced = true
		=}
	}
	else if(peekFront(noc[id].channels[buf].buffer) == id){
		{=
			// Mark the channel as serviced
			noc[id].channels[buf].serviced = true,
			// Remove this flit, because it has reached its destination
			noc[id].channels[buf].buffer = dequeue(noc[id].channels[buf].buffer)
		=}
	}
	else{
		AdvanceFlits(id, buf)
	}
}

// Update the directions of the next flits in the buffers
process AdvanceRouter(int id){
	AdvanceChannel(id, noc[id].priority_list[0]);
	AdvanceChannel(id, noc[id].priority_list[1]);
	AdvanceChannel(id, noc[id].priority_list[2]);
	AdvanceChannel(id, noc[id].priority_list[3]);
	AdvanceChannel(id, noc[id].priority_list[4])
}

process UpdatePriorityList(int id, int i){
	if(noc[id].channels[noc[id].priority_list[i]].serviced){
		{=
			0: noc[id].priority_list_temp[noc[id].total_unserviced + noc[id].serviced_index] = noc[id].priority_list[i],
			1: noc[id].serviced_index++
		=}
	}
	else{
		{=
			0: noc[id].priority_list_temp[noc[id].unserviced_index] = noc[id].priority_list[i],
			1: noc[id].unserviced_index++
		=}
	}
}

// Update priority list
process UpdatePiority(int id){
	// Reset all of the temp values
	{=
		noc[id].priority_list_temp = [0, 0, 0, 0, 0],
		noc[id].serviced_index = 0,
		noc[id].unserviced_index = 0
	=};
	// Update the ordering of priorities in the lists
	UpdatePriorityList(id, 0);
	UpdatePriorityList(id, 1);
	UpdatePriorityList(id, 2);
	UpdatePriorityList(id, 3);
	UpdatePriorityList(id, 4);

	{=
		// Make our next priority list the current priority list
		noc[id].priority_list = noc[id].priority_list_temp,

		// Reset the used and serviced variables
		noc[id].total_unserviced = 0,
		noc[id].used = [false, false, false, false],
		noc[id].channels[0].serviced = false,
		noc[id].channels[1].serviced = false,
		noc[id].channels[2].serviced = false,
		noc[id].channels[3].serviced = false,
		noc[id].channels[4].serviced = false
	=}
}

// For syncing parallell processes
process Clock(){
	tick
	{=
		CLK++
	=};
	if (CLK <= CLK_UPPER) {
	   Clock()
	}
	else {
	   stop
	}
}

// Process for Arbiter 1
process Arbiter(int id, int id_north, int id_west, int id_east, int id_south){
	// Initialize
	channel channel_init;
	{=
		// The initial channel values
		0: channel_init = channel {serviced: false},

		// Set up the router
		1: noc[id] = router {
			ids: 				   [id_north, id_west, id_east, id_south],
			priority_list:    [NORTH, EAST, WEST, SOUTH, LOCAL],
			used: 			   [false, false, false, false],
			total_unserviced:	0,
			channels:		   [channel_init, channel_init, channel_init, channel_init, channel_init]
		},
		2: noc[id].channels[LOCAL].buffer = enqueue(0, none)
	=};

	// Run
	do{
		// Send the flits and update priorities
		tick;
		AdvanceRouter(id);

		// Update the priority list
		tock;
		UpdatePiority(id);

		if(CLK>=CLK_UPPER){
			stop
		}
		else{
			tau
		}
	}
}



// ------------------------------------------------------- Execution --------------------------------------------------------

// These processes are run concurrently and kept in sync by tick and tock.
par{
	::	Clock()
	// #MODULARIZE by adding new arbiter processes for any additional routers.
	::  Arbiter(0, -1, -1,  1,  2)
	::  Arbiter(1, -1,  0, -1,  3)
	::  Arbiter(2,  0, -1,  3, -1)
	::  Arbiter(3,  1,  2, -1, -1)
}
