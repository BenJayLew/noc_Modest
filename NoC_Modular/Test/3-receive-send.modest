/*
 * This is a test for sending a flit from the local buffer of a router across multiple other routers and to its destination.
 *
 * NoC router ID map:
 *	0 - 1 - 2
 *	|   |   |
 *	3 - 4 - 5
 * |   |   |
 * 6 - 7 - 8
 *
 * Editors: Jonah Boe
 * Date: Jul 08, 2021
 */

option "dtmc";



// ------------------------------------------------------- Datatypes --------------------------------------------------------

datatype buffer = {int(0..NOC_MAX_ID) hd, buffer option tl};
datatype channel = {buffer option buffer, int(0..4) priority};
datatype router = {channel[] channels, int(-1..NOC_MAX_ID)[] ids};



// --------------------------------------------------- Top level varables ---------------------------------------------------

// #MODULARIZE by setting NOC_MAX_ID to 1 less than the total number of routers and allocating spaces for more routers
const int NOC_MAX_ID = 8;
router[] noc = [router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}];

const int BUFFER_LENGTH = 3;

const int NORTH = 0;
const int WEST = 1;
const int EAST = 2;
const int SOUTH = 3;
const int LOCAL = 4;

int (0..1) packetSent;



// ------------------------------------------------------- Properties -------------------------------------------------------

property Test1 = Pmax(<> (packetSent == 1));



// ------------------------------------------------------- Functions --------------------------------------------------------

// Calculate length of list
function int len(buffer option ls) = if ls == none then 0 else 1 + len(ls!.tl);

// Get the mirror direction
function int getDestinationDirection(int dir) =
	if dir == NORTH then SOUTH
	else if dir == WEST then EAST
	else if dir == EAST then WEST
	else if dir == SOUTH then NORTH
	else -1;

// Get the id of the first router in this row
function int firstInRow(int id) =
	if noc[id].ids[WEST] == -1 then id
	else firstInRow(noc[id].ids[WEST]);

// Get the id of the last router in this row
function int lastInRow(int id) =
	if noc[id].ids[EAST] == -1 then id
	else lastInRow(noc[id].ids[EAST]);

// Tell if the buffer is full
function bool isBufferFull(int id, int dir) = len(noc[id].channels[dir].buffer) >= BUFFER_LENGTH;

// Return the front of the queue
function int peekFront(buffer option ls) =
	if ls == none then -1
	else if ls!.tl == none then ls!.hd
	else peekFront(ls!.tl);

// Add a flit to the buffer
function buffer option enqueue(int n, buffer option ls) =
	if len(ls) == BUFFER_LENGTH then ls
	else some(buffer {
		hd: n,
		tl: ls
	});

// Remove a flit from the buffer
function buffer option dequeue(buffer option ls) =
	if ls == none then none
	else if	ls!.tl == none then none
	else some(buffer {
		hd: ls!.hd,
		tl: dequeue(ls!.tl)
	});



// ------------------------------------------------------- Processes --------------------------------------------------------

// Send flit to northern router. src and dst are channels (ie NORTH, WEST, EAST, SOUTH, LOCAL), NOT the actual router ids!
process Send(int id, int src, int dir){
	// We want to chech the final destination buffer of the router we are headed into, to see if it is full
	int(-1..3) dst;
	{=
		// A flit going from router 0 to router 1 leaves 0 from the SOUTH and enters 1 from the NORTH.
		dst = getDestinationDirection(dir)
	=};

	if(!isBufferFull(noc[id].ids[dir], dst)){
		{=
			// Add flit to destination buffer
			noc[noc[id].ids[dir]].channels[dst].buffer =
				enqueue(peekFront(noc[id].channels[src].buffer), noc[noc[id].ids[dir]].channels[dst].buffer),
			// Remove it from the source buffer
			noc[id].channels[src].buffer = dequeue(noc[id].channels[src].buffer)
		=}
	}
	else{
		tau
	}
}

// Advance flits to respective buffers
process AdvanceFlits(int id){
	// If flit needs to stay on this row
	// TODO: test using local variable here
	int flit;
	{=
		flit = peekFront(noc[id].channels[LOCAL].buffer)
	=};

	if(flit >= firstInRow(id) && flit <= lastInRow(id)){
		// Send it west
		if(flit < id){
			Send(id, LOCAL, WEST)
		}
		// Or send it east
		else{
			Send(id, LOCAL, EAST)
		}
	}
	// Else, if flit needs to go north
	else if(flit < id){
		Send(id, LOCAL, NORTH)
	}
	// Else, flit needs to go south
	else{
		Send(id, LOCAL, SOUTH)
	}
}

// Process for Arbiter 1
process Arbiter(int id, int id_north, int id_west, int id_east, int id_south){
	// Initialize
	{=
		noc[id] = router {ids: [id_north, id_west, id_east, id_south]},
		noc[id].channels =
			[channel {priority: 0}, channel {priority: 0}, channel {priority: 0}, channel {priority: 0}, channel {priority: 0}],

		// TEST: Create the routers 1 - 8
		noc[1] = router {ids: [-1,  0,  2,  4]},
		noc[1].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[2] = router {ids: [-1,  1, -1,  5]},
		noc[2].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[3] = router {ids: [ 0, -1,  4,  6]},
		noc[3].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[4] = router {ids: [ 1,  3,  5,  7]},
		noc[4].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[5] = router {ids: [ 2,  4, -1,  8]},
		noc[5].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[6] = router {ids: [ 3, -1,  7, -1]},
		noc[6].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[7] = router {ids: [ 4,  6,  8, -1]},
		noc[7].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],
		noc[8] = router {ids: [ 5,  7, -1, -1]},
		noc[8].channels = [channel {}, channel {}, channel {}, channel {}, channel {}],

		// TEST: Create a flit with destination of north router id
		noc[id].channels[LOCAL].buffer = enqueue(3, none)
	=};

	// Run
	do{
		// Advance the flit
		AdvanceFlits(id);

		if(peekFront(noc[3].channels[NORTH].buffer) != -1){
			{=
				packetSent = 1
			=}
		}
		else{
			tau
		};

		// End program
		stop
	}
}



// ------------------------------------------------------- Execution --------------------------------------------------------

Arbiter(0, -1, -1, 1, 3)
