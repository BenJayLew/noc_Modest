/****************************************************************************************************************************
 * This is a modularized version of the concrete model for NoC model verification.
 *
 * Current NoC router IDs
 *	0 - 1 - 2
 *	|   |   |
 *	3 - 4 - 5
 *  |   |   |
 *  6 - 7 - 8
 *
 * In order to add routers to the NoC visit all coments labled with #MODULARIZE
 *
 * Editors: Jonah Boe
 * Date: Dec 14, 2021
 * Designed using: The Modest Toolset (www.modestchecker.net), version v3.1.182-g3d5d3ecdf.
 ***************************************************************************************************************************/

option "dtmc";



// --------------------------------------------------- Top level varables --------------------------------------------------

// #MODULARIZE by setting NOC_MAX_ID to 1 less than the total number of routers and allocating spaces for more routers
const int NOC_MAX_ID = 8;
router[] noc = [router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}, router {}];

// #CUSTOMIZE this value changes the buffer length of all buffers.
const int BUFFER_LENGTH = 4;

// #CUSTOMIZE the number of buffers a router must service before noise will be incramented.
const int ACTIVITY_THRESH = 3;

// #CUSTOMIZE these values change the frequency with witch flits are injected.
const int INJECTION_RATE_NUMERATOR = 3;
const int INJECTION_RATE_DENOMINATOR = 10;

// #CUSTOMIZE this is the upper bound of cycles run before quiting
const int CLK_UPPER = 100;
int clk = 0;

// #CUSTOMIZE this is the upper threshoold for noise detected in the system
const int RESISTIVE_NOISE_THRESH = 1;
const int INDUCTIVE_NOISE_THRESH = 1;

// These are for tracking resistive and inductive noise and must be initialized to 0.
int resistiveNoise = 0;
int inductiveNoise = 0;

// These values are enumerations and are NOT to be changed
const int NORTH = 0;
const int WEST = 1;
const int EAST = 2;
const int SOUTH = 3;
const int LOCAL = 4;



// ------------------------------------------------------- Datatypes -------------------------------------------------------

datatype buffer = {
	int(0..NOC_MAX_ID) hd,
	buffer option tl
};

datatype channel = {
	buffer option buffer,
	bool serviced,
	bool isEmpty,
	bool isFull
};

datatype router = {
	channel[] channels,
	int(-1..NOC_MAX_ID)[] ids,
	int(0..4)[] priority_list,
	int(0..4)[] priority_list_temp,
	int(0..4) serviced_index,
	int(0..4) unserviced_index,
	int(0..5) total_unserviced,
	int thisActivity,
	int lastActivity,
	bool[] used
};



// ------------------------------------------------------ Properties -------------------------------------------------------

property resistiveNoiseProbability = Pmax(<> (resistiveNoise >= RESISTIVE_NOISE_THRESH));
//property inductiveNoiseProbability = Pmax(<> (inductiveNoise >= INDUCTIVE_NOISE_THRESH));



// -------------------------------------------------------- Actions ---------------------------------------------------------

// For keeping parallel processes synced
action tick;
action tock;



// ------------------------------------------------------- Functions --------------------------------------------------------

// Calculate length of list
function int len(buffer option ls) = if ls == none then 0 else 1 + len(ls!.tl);

// Tell if the buffer is full
function bool isBufferFull(buffer option ls) = len(ls) >= BUFFER_LENGTH;

// Get the mirror direction
function int getDestinationChannel(int dir) =
	if dir == NORTH then SOUTH
	else if dir == WEST then EAST
	else if dir == EAST then WEST
	else if dir == SOUTH then NORTH
	else -1;

// Determine if the flits destination is in the same column as the current router
function int getColumnShift(int id, int dst) =
   (dst % ((int)sqrt(NOC_MAX_ID + 1))) - (id % ((int)sqrt(NOC_MAX_ID + 1)));

// Return the front of the queue
function int peekFront(buffer option ls) =
	if ls == none then -1
	else if ls!.tl == none then ls!.hd
	else peekFront(ls!.tl);

// Add a flit to the buffer
function buffer option enqueue(int n, buffer option ls) =
	if len(ls) == BUFFER_LENGTH then ls
	else some(buffer {
		hd: n,
		tl: ls
	});

// Remove a flit from the buffer
function buffer option dequeue(buffer option ls) =
	if ls == none then none
	else if ls!.tl == none then none
	else some(buffer {
		hd: ls!.hd,
		tl: dequeue(ls!.tl)
	});



// ------------------------------------------------------- Processes --------------------------------------------------------

process GenerateFlits(int id){
	int(0..NOC_MAX_ID) destination;
	if((clk % INJECTION_RATE_DENOMINATOR) < INJECTION_RATE_NUMERATOR){
		// Determine a destiantion
		palt{
			// #MODULARIZE by adding a probability for (NOC_MAX_ID - 1) destinations.
			// #CUSTOMIZE by changing the flit injection pattern. Check router IDs with if-else blocks to make more router specific changes.
			:(1/8): {= destination = 0 =}
			:(1/8): {= destination = 1 =}
			:(1/8): {= destination = 2 =}
			:(1/8): {= destination = 3 =}
			:(1/8): {= destination = 4 =}
			:(1/8): {= destination = 5 =}
			:(1/8): {= destination = 6 =}
			:(1/8): {= destination = 7 =}
		};

		// Make sure it is not this router and add it to the local buffer.
		if(destination >= id){
			{=
				noc[id].channels[LOCAL].buffer = enqueue(destination + 1, noc[id].channels[LOCAL].buffer)
			=}
		}
		else{
			{=
				noc[id].channels[LOCAL].buffer = enqueue(destination, noc[id].channels[LOCAL].buffer)
			=}
		}
	}
	else {
		tau
	}
}

process PrepChannel(int id, int ch){
	if(len(noc[id].channels[ch].buffer) == 0){
		{=
			noc[id].channels[ch].isEmpty = true
		=}
	}
	else{
		{=
			noc[id].channels[ch].isEmpty = false
		=}
	};
	if(isBufferFull(noc[id].channels[ch].buffer)){
		{=
			noc[id].channels[ch].isFull = true
		=}
	}
	else{
		{=
			noc[id].channels[ch].isFull = false
		=}
	}
}

process PrepRouter(int id){
	PrepChannel(id, 0);
	PrepChannel(id, 1);
	PrepChannel(id, 2);
	PrepChannel(id, 3);
	PrepChannel(id, 4)
}

// Send flit to northern router. src and dst are channels (ie NORTH, WEST, EAST, SOUTH, LOCAL), NOT the actual router ids!
process Send(int id, int buf, int dir){
	// We want to chech the final destination buffer of the router we are headed into, to see if it is full
	int(-1..3) dst;
	{=
		// A flit going from router 0 to router 1 leaves 0 from the SOUTH and enters 1 from the NORTH.
		dst = getDestinationChannel(dir)
	=};

	// If the destination is not full, and the channel has not been used in this cycle, then service the buffer
	if(!noc[noc[id].ids[dir]].channels[dst].isFull && !noc[id].used[dir]){
		{=
			// First, add flit to destination buffer
			0: noc[noc[id].ids[dir]].channels[dst].buffer =
				enqueue(peekFront(noc[id].channels[buf].buffer), noc[noc[id].ids[dir]].channels[dst].buffer),
			
			// Then, remove it from the source buffer
			1: noc[id].channels[buf].buffer = dequeue(noc[id].channels[buf].buffer),
			
			// Mark that output as used and that channel as serviced
			1: noc[id].used[dir] = true,
			1: noc[id].channels[buf].serviced = true,
			
			// Incrament the count for buffers serviced in this cycle
			1: noc[id].thisActivity++
		=}
	}
	// Otherwise, increment total unserviced buffers and set resistive noise to false
	else{
		{=
			noc[id].total_unserviced++
		=}
	}
}

// Advance flits to respective buffers
process AdvanceFlits(int id, int buf){
	// If flit needs to stay on this row
	if(getColumnShift(id, peekFront(noc[id].channels[buf].buffer)) == 0){
		// Send it north
		if(peekFront(noc[id].channels[buf].buffer) < id){
			Send(id, buf, NORTH)
		}
		// Or send it south
		else{
			Send(id, buf, SOUTH)
		}
	}
	// Else, if flit needs to go west
	else if(getColumnShift(id, peekFront(noc[id].channels[buf].buffer)) < 0){
		Send(id, buf, WEST)
	}
	// Else, flit needs to go east
	else{
		Send(id, buf, EAST)
	}
}

// Update a specific channel
process AdvanceChannel(int id, int buf){
	// If this channel was not assigned a neighbor...
	if(buf != 4 && noc[id].ids[buf] == -1){
		{=
			// Mark the channel as serviced
			noc[id].channels[buf].serviced = true
		=}
	}
	// If this channel was assigned a neighbor or is local, but it is empty...
	else if(noc[id].channels[buf].isEmpty == true){
		{=
			// Mark the channel as serviced
			noc[id].channels[buf].serviced = true
		=}
	}
	// If the flit at this channel has the same ID as this router...
	else if(peekFront(noc[id].channels[buf].buffer) == id){
		{=
			// Mark the channel as serviced
			noc[id].channels[buf].serviced = true,

			// Incrament the count for buffers serviced in this cycle
			noc[id].thisActivity++,

			// Remove this flit. It has reached its destination
			noc[id].channels[buf].buffer = dequeue(noc[id].channels[buf].buffer)
		=}
	}

	// Otherwise, the flit must be for another router
	else{
		AdvanceFlits(id, buf)
	}
}

// Update the directions of the next flits in the buffers
process AdvanceRouter(int id){
	AdvanceChannel(id, noc[id].priority_list[0]);
	AdvanceChannel(id, noc[id].priority_list[1]);
	AdvanceChannel(id, noc[id].priority_list[2]);
	AdvanceChannel(id, noc[id].priority_list[3]);
	AdvanceChannel(id, noc[id].priority_list[4])
}

process UpdatePriorityList(int id, int i){
	if(noc[id].channels[noc[id].priority_list[i]].serviced){
		{=
			0: noc[id].priority_list_temp[noc[id].total_unserviced + noc[id].serviced_index] = noc[id].priority_list[i],
			1: noc[id].serviced_index++
		=}
	}
	else{
		{=
			0: noc[id].priority_list_temp[noc[id].unserviced_index] = noc[id].priority_list[i],
			1: noc[id].unserviced_index++
		=}
	}
}

// Update priority list
process UpdatePiority(int id){
	// Reset all of the temp values
	{=
		noc[id].priority_list_temp = [0, 0, 0, 0, 0],
		noc[id].serviced_index = 0,
		noc[id].unserviced_index = 0
	=};
	// Update the ordering of priorities in the lists
	UpdatePriorityList(id, 0);
	UpdatePriorityList(id, 1);
	UpdatePriorityList(id, 2);
	UpdatePriorityList(id, 3);
	UpdatePriorityList(id, 4);

	{=
		// Make our next priority list the current priority list
		noc[id].priority_list = noc[id].priority_list_temp,

		// Reset the used and serviced variables
		noc[id].channels[0].serviced = false,
		noc[id].channels[1].serviced = false,
		noc[id].channels[2].serviced = false,
		noc[id].channels[3].serviced = false,
		noc[id].channels[4].serviced = false,
		noc[id].total_unserviced = 0,
		noc[id].used = [false, false, false, false]
	=}
}

// Update the noise tracking variables for this router.
process UpdateNoiseTracking(int id){
	// Update inductive noise
	if (abs(noc[id].lastActivity - noc[id].thisActivity) >= ACTIVITY_THRESH) {
		{=
			inductiveNoise++
		=}
	}
	else{
		tau
	};
	// Update resistive noise
	if(noc[id].thisActivity >= ACTIVITY_THRESH){
		{=
			resistiveNoise++
		=}
	}
	else{
		tau
	};

	// Update trackers for next round
	{=
		0: noc[id].lastActivity = noc[id].thisActivity,
		1: noc[id].thisActivity = 0
	=}
}

// Process for behavior
process RouterBehavior(int id, int id_north, int id_west, int id_east, int id_south){
	// Initialize the router
	{=
		noc[id] = router {
			channels: [
				channel {serviced: false, isEmpty: false},
				channel {serviced: false, isEmpty: false},
				channel {serviced: false, isEmpty: false},
				channel {serviced: false, isEmpty: false},
				channel {serviced: false, isEmpty: false}],
			ids: [id_north, id_west, id_east, id_south],
			priority_list: [NORTH, EAST, SOUTH, WEST, LOCAL],
			total_unserviced: 0,
			used: [false, false, false, false],
			thisActivity: 0,
			lastActivity: 0
		}
	=};

	// Run
	do{
		tick;
		// Generate new flits
		GenerateFlits(id);
		// Determine what channels will send this cycle
		PrepRouter(id);

		tock;
		// Send the flits
		AdvanceRouter(id);
		// Update the priority list
		UpdatePiority(id);
		// Update resistive and inductive values
		UpdateNoiseTracking(id);

		if(clk >= CLK_UPPER){
			stop
		}
		else{
			tau
		}
	}
}

// For syncing parallell processes
process Clock(){
	tick
	{=
		clk++
	=};
	if (clk <= CLK_UPPER) {
	   Clock()
	}
	else {
	   stop
	}
}



// ------------------------------------------------------- Execution --------------------------------------------------------

// These processes are run concurrently and kept in sync by tick and tock.
par{
	::	Clock()
	// #MODULARIZE by adding new arbiter processes for any additional routers.
	//	0 - 1 - 2
	//	|   |   |
	//	3 - 4 - 5
	//  |   |   |
	//  6 - 7 - 8
	:: RouterBehavior(0, -1, -1,  1,  3)
	:: RouterBehavior(1, -1,  0,  2,  4)
	:: RouterBehavior(2, -1,  1, -1,  5)
	:: RouterBehavior(3,  0, -1,  4,  6)
	:: RouterBehavior(4,  1,  3,  5,  7)
	:: RouterBehavior(5,  2,  4, -1,  8)
	:: RouterBehavior(6,  3, -1,  7, -1)
	:: RouterBehavior(7,  4,  6,  8, -1)
	:: RouterBehavior(8,  5,  7, -1, -1)
}
